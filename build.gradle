
plugins {
    id 'java'
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.gradle.jacoco'
    id 'com.gradle.plugin-publish' version '0.9.7'
    id 'org.sonarqube' version '2.5'
}

version = '0.6-SNAPSHOT'
group = 'com.github.rodm'

repositories {
    mavenCentral()
    maven {
        url = 'http://download.jetbrains.com/teamcity-repository'
    }
}

sourceSets {
    v10
    v2017

    main {
        compileClasspath += v10.output + v2017.output
        runtimeClasspath += v10.output + v2017.output
    }

    functionalTest {
        compileClasspath += main.output + configurations.testRuntime
        runtimeClasspath += main.output + configurations.testRuntime
        groovy {
            srcDir 'src/functional/groovy'
        }
    }
}

dependencies {
    v10CompileOnly('org.jetbrains.teamcity:configs-dsl-server:10.0.0') {
        exclude module: 'groovy-all'
    }
    v10CompileOnly 'org.jetbrains.teamcity.internal:server:10.0'

    v2017CompileOnly('org.jetbrains.teamcity:configs-dsl-server:2017.1') {
        exclude module: 'groovy-all'
    }
    v2017CompileOnly 'org.jetbrains.teamcity.internal:server:2017.1'

    compileOnly 'org.jetbrains.teamcity.idea:annotations:132.719'

    testCompile 'junit:junit:4.12'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

compileJava {
    sourceCompatibility = 1.8
    targetCompatibility = 1.8
}

jar {
    from sourceSets.v10.output
    from sourceSets.v2017.output
}

gradlePlugin {
    testSourceSets sourceSets.functionalTest
}

task functionalTest(type: Test) {
    description = 'Runs the functional tests.'
    group = 'verification'
    testClassesDir = sourceSets.functionalTest.output.classesDir
    classpath = sourceSets.functionalTest.runtimeClasspath
    systemProperty 'teamcity.server.host', project.findProperty('teamcity.server.host') ?: 'localhost'

    reports {
        html.destination = project.file("$html.destination/functional")
        junitXml.destination = project.file("$junitXml.destination/functional")
    }
}

publishing {
    repositories {
        mavenLocal()
    }

    publications {
        plugin(MavenPublication) {
            from components.java
        }
    }
}

pluginBundle {
    website = 'https://github.com/rodm/gradle-teamcity-dsl-plugin'
    vcsUrl = 'https://github.com/rodm/gradle-teamcity-dsl-plugin'
    description = 'Gradle plugin for generating TeamCity settings from a Kotlin DSL'
    tags = ['teamcity', 'dsl', 'kotlin']

    plugins {
        plugin {
            id = 'com.github.rodm.teamcity-dsl'
            displayName = 'Gradle TeamCity DSL plugin'
        }
    }

    mavenCoordinates {
        groupId = project.group
        artifactId = project.name
    }
}
